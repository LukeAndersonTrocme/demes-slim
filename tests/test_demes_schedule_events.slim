initialize() {
    if (!exists("JSON_FILE")) {
        stop("must specify json file");
    }
    if (!exists("SCALING_FACTOR")) {
        defineConstant("SCALING_FACTOR", 1.0);
    } 
    initializeMutationRate(0);
    initializeMutationType("m1", 0.5, "f", 0.0);
    initializeGenomicElementType("g1", m1, 1.0);
    initializeGenomicElement(g1, 0, 99);
    initializeRecombinationRate(0);
    source("../demes.slim", chdir=T);
}

1 {
    model = demes_load(JSON_FILE, scaling_factor=SCALING_FACTOR, burn_in=0.1);
    demes_schedule_events(model, verbosity=0);
    end_time = model.getValue("end_time");
    sim.registerLateEvent(NULL, "{sim.simulationFinished();}", end_time, end_time);
}

1: late() {
    for (p in sim.subpopulations) {
        ancestor_id = p.getValue("ancestor_id");
        if (isNULL(ancestor_id)) {
            ancestor_id = -1;
        }
        catn(c("#state",
               sim.generation,
               p.id,
               ancestor_id,
               p.individualCount,
               p.selfingRate,
               p.cloningRate,
               p.immigrantSubpopIDs,
               p.immigrantSubpopFractions));
    }
}
